# Maintainer: Jasmin <theblazehen@gmail.com>
pkgname=claude-notify
pkgver=1.3.0
pkgrel=1
pkgdesc="Cross-platform desktop notifications for Claude Code"
arch=('any')
url="https://github.com/mylee04/claude-notify"
license=('MIT')
depends=('bash' 'libnotify')
optdepends=('jq: for JSON parsing in status detection')
# Upstream renamed to code-notify in v1.3.0
_upstream=code-notify
source=("${_upstream}-${pkgver}.tar.gz::https://github.com/mylee04/claude-notify/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('2f96994e1fa4183726208bddb9952eac2c6156a8948763de97f1792279191a33')

package() {
    cd "${_upstream}-${pkgver}"

    # Install library files
    install -dm755 "$pkgdir/usr/share/$pkgname/lib"
    cp -r lib/${_upstream}/* "$pkgdir/usr/share/$pkgname/lib/"

    # Install main binary with patched path
    install -Dm755 bin/${_upstream} "$pkgdir/usr/bin/claude-notify"
    sed -i 's|LIB_DIR="$(dirname "$SCRIPT_DIR")/lib/code-notify"|LIB_DIR="/usr/share/claude-notify/lib"|' \
        "$pkgdir/usr/bin/claude-notify"

    # Create symlinks
    ln -s claude-notify "$pkgdir/usr/bin/cn"
    ln -s claude-notify "$pkgdir/usr/bin/cnp"

    # Install completions if present
    if [[ -d completions ]]; then
        install -Dm644 completions/bash/* -t "$pkgdir/usr/share/bash-completion/completions/" 2>/dev/null || true
        install -Dm644 completions/zsh/* -t "$pkgdir/usr/share/zsh/site-functions/" 2>/dev/null || true
    fi

    # Install license
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
